// This is used to test the performance of the federated runtime in decentralized mode
// using physical connections (number of reactions per second).
// See [Benchmarks wiki page](https://github.com/icyphy/lingua-franca/wiki/Benchmarks).
// Correct output for this 1, 2, 3, 4.
// Failure for this test is failing to halt or getting the wrong data.
target C {
  coordination: decentralized,
  timeout: 10 secs
}

reactor Clock(offset: time = 0, period: time = 1 sec) {
  output y: int
  timer t(offset, period)
  state count: int = 0

  reaction(t) -> y {=
    (self->count)++;
    lf_set(y, self->count);
  =}
}

reactor Destination {
  input x: int
  state s: int = 1

  reaction(x) {=
    // printf("%d\n", x->value);
    if (x->value != self->s) {
        lf_print_warning("Expected %d and got %d.", self->s, x->value);
    }
    self->s++;
  =}

  reaction(shutdown) {=
    lf_print("**** shutdown reaction invoked.");
    if (self->s != 10000002) {
        lf_print_warning("Expected 10000002 but got %d.", self->s);
    }
    lf_print("Approx. time per reaction: %lldns", lf_time_physical_elapsed()/(self->s+1));
  =}
}

federated reactor TimeLimitDistributedPhysical(period: time = 1 usec) {
  c = new Clock(period=period)
  d = new Destination()
  c.y ~> d.x
}
